//Bibliotecas
#Include "tlpp-core.th"
#Include "tlpp-rest.th"

//Declaração da namespace
Namespace atualizacaoCompleta

//Constantes
#Define CRLF Chr(13) + Chr(10) //Carriage Return Line Feed
  
/*/{Protheus.doc} User Function WSGrupoProdNovAtualizaRegistro
Exemplo de WS para atualizar um grupo de produto conforme body
@type  Function
@author Atilio
@since 26/03/2024
@obs Exemplo de conteúdo a vir no body
    * 1: Para campos do tipo Numérico, informe o valor sem usar as aspas
    * 2: Para campos do tipo Data, informe uma string no padrão 'YYYY-MM-DD'

    {
        "grupo": "conteudo",
        "desc": "conteudo",
        "proori": "conteudo",
        "status": "conteudo"
    }
/*/
  
@Put("WsGrupoProdNov/atualizacao/completa/unico/")
User Function WSGrupoProdNovAtualizaRegistro()
    Local jResponse         := JsonObject():New()     As Json
    Local jBody                                       As Json
    Local cAliasWS          := 'SBM'                  As Character
    Local aDados            := {}                     As Array
    Local nLinha            := 0                      As Numeric
    Local cDirLog           := '\x_logs\'             As Character
    Local cArqLog           := ''                     As Character
    Local cErrorLog         := ''                     As Character
    Local aLogAuto          := {}                     As Array
    Local cBody             := oRest:getBodyRequest() As Character
    Private lMsErroAuto     := .F.                    As Logical
    Private lMsHelpAuto     := .T.                    As Logical
    Private lAutoErrNoFile  := .T.                    As Logical

    //Aciona a preparação do ambiente
    preparaAmbiente.u_zPreparaAmbienteRest()

    //Se veio um body
    If ! Empty(cBody)

        //Busca os dados que o usuário informou
        jBody := JsonObject():New()
        jBody:fromJson(cBody)
    
        //Caso haja conteúdo
        If jBody != Nil .And. ! Empty(jBody:GetJsonObject('grupo'))
    
            DbSelectArea(cAliasWS)
            (cAliasWS)->(DbSetOrder(1))

            //Se não encontrar o registro
            If ! (cAliasWS)->(MsSeek(FWxFilial(cAliasWS) + jBody:GetJsonObject('grupo') ))
                oRest:setStatusCode(500)
                jResponse['errorId']  := 'ALT004'
                jResponse['error']    := 'Grupo nao encontrado'
                jResponse['solution'] := 'Codigo do Grupo nao encontrado na tabela ' + cAliasWS + ', portanto nao pode ser alterado'
    
            //Define o retorno
            Else

                //Adiciona os dados do ExecAuto
                aAdd(aDados, {'BM_GRUPO',   jBody:GetJsonObject('grupo')  ,   Nil})
                If ! Empty(jBody:GetJsonObject('desc'))
                    aAdd(aDados, {'BM_DESC',    jBody:GetJsonObject('desc')   ,   Nil})
                EndIf
                If ! Empty(jBody:GetJsonObject('proori'))
                    aAdd(aDados, {'BM_PROORI',  jBody:GetJsonObject('proori') ,   Nil})
                EndIf
                If ! Empty(jBody:GetJsonObject('status'))
                    aAdd(aDados, {'BM_STATUS',  jBody:GetJsonObject('status') ,   Nil})
                EndIf

                //Chama a exclusão automática
                MsExecAuto({|x, y| MATA035(x, y)}, aDados, 4)

                //Se houve erro, gera um arquivo de log dentro do diretório da protheus data
                If lMsErroAuto

                    //Monta o texto do Error Log que será salvo
                    cErrorLog   := ''
                    aLogAuto    := GetAutoGrLog()
                    For nLinha := 1 To Len(aLogAuto)
                        cErrorLog += aLogAuto[nLinha] + CRLF
                    Next nLinha

                    //Se a pasta de Logs não existir, cria ela
                    If ! ExistDir(cDirLog)
                        MakeDir(cDirLog)
                    EndIf

                    //Grava o arquivo de log
                    cArqLog := 'SBM_post_' + dToS(Date()) + '_' + StrTran(Time(), ':', '-') + '.log'
                    MemoWrite(cDirLog + cArqLog, cErrorLog)

                    //Define o retorno para o WebService
                    oRest:setStatusCode(500)
                    jResponse['errorId']  := 'ALT002'
                    jResponse['error']    := 'Erro na alteracao do registro'
                    jResponse['solution'] := 'Nao foi possivel alterar o registro, foi gerado um arquivo de log em ' + cDirLog + cArqLog + ' '

                //Senão, define o retorno
                Else
                    jResponse['note']     := 'Registro alterado com sucesso'
                EndIf
            EndIf
        Else
            //Define o retorno para o WebService
            oRest:setStatusCode(500)
            jResponse['errorId']  := 'ALT003'
            jResponse['error']    := 'Falha na conversao string para JSON e ou codigo do grupo vazio'
            jResponse['solution'] := 'Nao foi possivel capturar o Body e transformar em JSON, contate o Administrador ou o codigo do grupo informado esta vazio'
        EndIf
    
    Else
        oRest:setStatusCode(500)
        jResponse['errorId']  := 'NOV001'
        jResponse['error']    := 'Body vazio'
        jResponse['solution'] := 'Informe o Body da requisicao'
    EndIf
  
    //Encerra retornando o JSON
    oRest:setKeyHeaderResponse('Content-Type','application/json')
    oRest:setResponse(jResponse:toJSON())
Return
