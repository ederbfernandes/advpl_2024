//Bibliotecas
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "TopConn.ch"

//Declaração da namespace
Namespace usandoGetVarios

//Constantes
#Define CRLF Chr(13) + Chr(10) //Carriage Return Line Feed
  
/*/{Protheus.doc} User Function WSGrupoProdNovGetTodos
Exemplo de WS para buscar vários grupos de produto com paginação
@type  Function
@author Atilio
@since 26/03/2024
@obs Poderia ser usado o FWAdapterBaseV2(), mas em algumas versões antigas não existe essa funcionalidade
    então a paginação foi feita manualmente

    Parâmetros a receber na requisição:
    initId = Código Inicial do Grupo de Produtos
    lastId = Código Final do Grupo de Produtos
    limit = Quantidade de registros por página
    page = Número da página da navegação
/*/
  
@Get("WsGrupoProdNov/get/todos/")
User Function WSGrupoProdNovGetTodos()
    Local jResponse := JsonObject():New() As Json
    Local jPath                           As Json
    Local cAliasWS  := 'SBM'              As Character
    Local cQueryTab := ''                 As Character
    Local nTamanho  := 10                 As Numeric
    Local nTotal    := 0                  As Numeric
    Local nPags     := 0                  As Numeric
    Local nPagina   := 0                  As Numeric
    Local nAtual    := 0                  As Numeric
    Local jRegistro                       As Json

    //Aciona a preparação do ambiente
    preparaAmbiente.u_zPreparaAmbienteRest()

    //Busca os dados que o usuário informou
    jPath := JsonObject():New()
    jPath := oRest:getQueryRequest()

    //Caso haja conteúdo
    If jPath != Nil
        //Gravando log de como que esta o jPath
        //MemoWrite("\spool\jPath.html", VarInfo("jPath", jPath))

        //Efetua a busca dos registros
        cQueryTab := " SELECT " + CRLF
        cQueryTab += "     TAB.R_E_C_N_O_ AS TABREC " + CRLF
        cQueryTab += " FROM " + CRLF
        cQueryTab += "     " + RetSQLName(cAliasWS) + " TAB " + CRLF
        cQueryTab += " WHERE " + CRLF
        cQueryTab += "     TAB.D_E_L_E_T_ = '' " + CRLF
        
        //Se tem o código inicial
        If ! Empty(jPath:GetJsonObject('initId'))
            cQueryTab += "     AND BM_GRUPO >= '" + jPath:GetJsonObject('initId') + "' " + CRLF
        EndIf

        //Se tem o código final
        If ! Empty(jPath:GetJsonObject('lastId'))
            cQueryTab += "     AND BM_GRUPO <= '" + jPath:GetJsonObject('lastId') + "' " + CRLF
        EndIf

        cQueryTab += " ORDER BY " + CRLF
        cQueryTab += "     TABREC " + CRLF
        TCQuery cQueryTab New Alias 'QRY_TAB'

        //Se não encontrar registros
        If QRY_TAB->(EoF())
            oRest:setStatusCode(500)
            jResponse['errorId']  := 'ALL002'
            jResponse['error']    := 'Registro(s) não encontrado(s)'
            jResponse['solution'] := 'A consulta de registros não retornou nenhuma informação'
        Else
            jResponse['objects'] := {}

            //Conta o total de registros
            Count To nTotal
            QRY_TAB->(DbGoTop())

            //O tamanho do retorno, será o limit, se ele estiver definido
            If ! Empty(jPath:GetJsonObject('limit'))
                nTamanho := Val(jPath:GetJsonObject('limit'))
            EndIf

            //Pegando total de páginas
            nPags := NoRound(nTotal / nTamanho, 0)
            nPags += Iif(nTotal % nTamanho != 0, 1, 0)
            
            //Se vier página
            If ! Empty(jPath:GetJsonObject('page'))
                nPagina := Val(jPath:GetJsonObject('page'))
            EndIf

            //Se a página vier zerada ou negativa ou for maior que o máximo, será 1 
            If nPagina <= 0 .Or. nPagina > nPags
                nPagina := 1
            EndIf

            //Se a página for diferente de 1, pula os registros
            If nPagina != 1
                QRY_TAB->(DbSkip((nPagina-1) * nTamanho))
            EndIf

            //Adiciona os dados para a meta
            jJsonMeta := JsonObject():New()
            jJsonMeta['total']         := nTotal
            jJsonMeta['current_page']  := nPagina
            jJsonMeta['total_page']    := nPags
            jJsonMeta['total_items']   := nTamanho
            jResponse['meta'] := jJsonMeta

            //Percorre os registros
            While ! QRY_TAB->(EoF())
                nAtual++
                
                //Se ultrapassar o limite, encerra o laço
                If nAtual > nTamanho
                    Exit
                EndIf

                //Posiciona o registro e adiciona no retorno
                DbSelectArea(cAliasWS)
                (cAliasWS)->(DbGoTo(QRY_TAB->TABREC))
                
                jRegistro := JsonObject():New()
                jRegistro['grupo'] := (cAliasWS)->BM_GRUPO 
                jRegistro['desc'] := (cAliasWS)->BM_DESC 
                aAdd(jResponse['objects'], jRegistro)

                QRY_TAB->(DbSkip())
            EndDo
        EndIf
        QRY_TAB->(DbCloseArea())
  
    //Do contrário, irá retornar uma mensagem que não foi encontrado
    Else
        oRest:setStatusCode(500)
        jResponse['errorId']  := 'ALL001'
        jResponse['error']    := 'Objeto vazio'
        jResponse['solution'] := 'Falha ao executar, contate o Administrador'
    EndIf
  
    //Encerra retornando o JSON
    oRest:setKeyHeaderResponse('Content-Type','application/json')
    oRest:setResponse(jResponse:toJSON())
Return
